      FUNCTION DIF (L,M,NP,VARI,VARD)
C
C     THIS FUNCTION IS CALLED BY SUBROUTINE GEOM TO CALCULATE THE
C     NOZZLE WALL SLOPE FOR THE TABULAR INPUT CASE. FUNCTION DIF WAS
C     TAKEN FROM THE NASA-LANGLEY PROGRAM LIBRARY.
C
C     THIS FUNCTION SUBPROGRAM FINDS THE DERIVATIVE AT A GIVEN POINT L
C     FOR THE DESIRED X AND Y IN A GIVEN TABLE. THE N-POINT LAGRANGIAN
C     FORMULA IS USED WHERE N IS ODD.
C
C     L    = INTEGER, POINT OF X AND Y AT WHICH DERIVATIVE IS FOUND
C     M    = INTEGER, 1-5, TO DETERMINE THE POINT FORMULA, N=2*M+1
C     NP   = INTEGER, NUMBER OF POINTS IN TABLE OF VARIABLES
C     VARI = ARRAY OF INDEPENDENT VARIABLE, X, VARI(NP)
C     VARD = ARRAY OF DEPENDENT VARIABLE,   Y, VARD(NP)
C
C     OCR note:
C     Reconstructed from Appendix C listing text blocks; numeric logic
C     preserved and comments normalized for readability.
C
      DIMENSION VARI(NP),VARD(NP),X(11),Y(11)
C
      DIF=1.777E19
      IF (M.LT.1) RETURN
      N=2*M+1
      IF (M.GT.5.OR.N.GT.NP) RETURN
      M1=M+1
      M2=NP-M1
      K=L
      IF (L.LE.M1.OR.N.EQ.NP) GO TO 10
      K=M1
      IF (L.LT.M2) GO TO 10
      K=L-(NP-N)
10    MX=L-K
      DO 20 J=1,N
      MJ=MX+J
      X(J)=VARI(MJ)
20    Y(J)=VARD(MJ)
      A=1.0
      B=0.0
      C=0.0
      DO 40 J=1,N
      IF (J.EQ.K) GO TO 40
      P=1.0
      DO 30 I=1,N
      IF (I.EQ.J) GO TO 30
      P=P*(X(J)-X(I))
30    CONTINUE
      T=X(K)-X(J)
      B=B+Y(J)/(P*T)
      A=A*T
      C=C+1.0/T
40    CONTINUE
      DIF=A*B+Y(K)*C
      RETURN
      END
